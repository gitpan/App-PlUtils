#!perl

our $DATE = '2014-11-09'; # DATE
our $VERSION = '0.03'; # VERSION

use 5.010;
use strict;
use warnings;

use App::PMUtils;
use Perinci::CmdLine::Any -prefer_lite=>1;

our %SPEC;
$SPEC{pllex} = {
    v => 1.1,
    summary => 'Tokenize Perl script (or module) using Compiler::Lexer',
    description => <<'_',



_
    args => {
        module => {
            schema => 'str*',
            completion => $App::PMUtils::_complete_module,
            cmdline_aliases => {m=>{}},
        },
        file => {
            summary => 'Specify file instead of module',
            schema  => 'str*',
            pos     => 0,
        },
    },
    result_naked=>1,
};
sub pllex {
    require Compiler::Lexer;
    require SHARYANTO::Module::Path;

    my %args = @_;
    my $path;
    if (defined $args{module}) {
        my $mod = $args{module};
        $mod =~ s/\.pm$//; $mod =~ s!/!::!g; # convenience
        $path = SHARYANTO::Module::Path::module_path(module=>$mod);
        die "Can't find module '$mod'" unless $path;
    } elsif (defined $args{file}) {
        $path = $args{file};
        (-f $path) or die "No such file '$path'\n";
    } else {
        die "Please specify file or module (-m)\n";
    }

    my $lexer = Compiler::Lexer->new($path);
    my $ct = do {
        undef $/;
        open my $fh, "<", $path or die "Can't open '$path': $!\n";
        <$fh>;
    };
    my $tokens = $lexer->tokenize($ct);
    my @res;
    for (@$tokens) { push @res, { %$_ } }
    \@res;
}

Perinci::CmdLine::Any->new(
    url => '/main/pllex',
)->run;

# ABSTRACT: Tokenize Perl script (or module) using Compiler::Lexer
# PODNAME: pllex

__END__

=pod

=encoding UTF-8

=head1 NAME

pllex - Tokenize Perl script (or module) using Compiler::Lexer

=head1 VERSION

This document describes version 0.03 of pllex (from Perl distribution App-PlUtils), released on 2014-11-09.

=head1 SYNOPSIS

 % pllex Some/script.pl
 % pllex --module Some::Module

Example output:

 % cat 1.pl
 require A::B;
 require C;

 % pllex 1.pl
 +---------+--------------+------+------+-------------------+-------+------+
 | data    | has_warnings | kind | line | name              | stype | type |
 | require | 0            | 3    | 1    | RequireDecl       | 0     | 71   |
 | A       | 0            | 25   | 1    | Namespace         | 0     | 127  |
 | ::      | 0            | 1    | 1    | NamespaceResolver | 0     | 126  |
 | B       | 0            | 25   | 1    | Namespace         | 0     | 127  |
 | ;       | 0            | 21   | 1    | SemiColon         | 0     | 106  |
 | require | 0            | 3    | 2    | RequireDecl       | 0     | 71   |
 | C       | 0            | 16   | 2    | RequiredName      | 0     | 95   |
 | ;       | 0            | 21   | 2    | SemiColon         | 0     | 106  |
 +---------+--------------+------+------+-------------------+-------+------+

For more options:

 % pllex --help

=head1 BASH COMPLETION

This script has bash completion capability.

To activate bash completion for this script, put:

 complete -C pllex pllex

in your bash startup (e.g. C<~/.bashrc>). Your next shell session will then recognize tab completion for the command. Or, you can also directly execute the line above in your shell to activate immediately.

You can also install L<App::BashCompletionProg> which makes it easy to add completion for Perinci::CmdLine-based scripts. Just execute C<bash-completion-prog> and the C<complete> command will be added in your C<~/.bash-completion-prog>. Your next shell session will then recognize tab completion for the command.

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-PlUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/perlancar/perl-App-PlUtils>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-PlUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2014 by perlancar@cpan.org.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
